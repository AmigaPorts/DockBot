.key HELP/S,CLEAN/S,CPU/K,NOEXE/S,NOLIB/S,NOCLASSES/S,NOPREFS/S,DEBUGMEM/S,DEBUG/S,DIR/K

IF <HELP$NOHELP> EQ HELP
  ECHO "Options: "
  ECHO "  HELP/S       Show this help message."
  ECHO "  CLEAN/S      Remove object files and binaries."
  ECHO "  CPU/K        Sets the CPU to target. Defaults to CPU of this machine."
  ECHO "  NOEXE/S      Do not compile the DockBot executable."
  ECHO "  NOLIB/S      Do not compile dockbot.library."
  ECHO "  NOCLASSES/S  Do not compile loadable gadget modules."
  ECHO "  NOPREFS/S    Do not compile the preferences application."
  ECHO "  DEBUGMEM/S   Output memory debugging information."
  ECHO "  DEBUG/S      Compile with debug symbols."
  ECHO "  DIR/K        Set the output directory. Defaults to /bin."
  ECHO
  QUIT
ENDIF

ECHO Checking Requirements

ECHO NOLINE "  SAS/C: "
IF NOT EXISTS sc:c/sc
  ECHO Not Found
  QUIT
ENDIF
VERSION sc:c/sc 6 58 >NIL:
IF WARN
  ECHO Need version 6.58
  QUIT
ENDIF
ECHO OK

ECHO NOLINE "  icon.library 46: "
VERSION icon.library 46 >NIL:
IF WARN
  ECHO Not Found
  QUIT
ENDIF
ECHO OK

ECHO NOLINE "  wbrun: "
IF NOT EXISTS c:wbrun 
  ECHO Not Found
  QUIT
ENDIF
ECHO OK

ECHO " "

; Decide on a CPU to compile for.
CPU CHECK 68040 >NIL:
IF WARN 
  CPU CHECK 68030 >NIL:
  IF WARN
    CPU CHECK 68020 >NIL:
    IF WARN
      SET defcpu 68000
    ELSE
      SET defcpu 68020
    ENDIF
  ELSE
    SET defcpu 68030
  ENDIF
ELSE
  SET defcpu 68040
ENDIF

.def CPU $defcpu

ECHO Compiling for <CPU>
ECHO " "

IF <DEBUGMEM$NODEBUGMEM> EQ DEBUGMEM
  ECHO Enabling Memory Debug Output
  ECHO " "
  SET DEFDEBUGMEM "define=DEBUG_MEM"
ELSE
  SET DEFDEBUGMEM "define=NO_DEBUG_MEM"
ENDIF

IF <DEBUG$NODEBUG> EQ DEBUG
  ECHO Compiling With Debug Symbols
  ECHO " "
  SET DEFDEBUG "debug=full"
ELSE
  SET DEFDEBUG "nodebug"
ENDIF

.def DIR /bin

ECHO "Output to <DIR>"
ECHO " "

IF NOT EXISTS <DIR>
  MAKEDIR <DIR>
ENDIF

IF <NOLIB$LIB> EQ LIB
  ECHO Compiling dockbot.library
  ECHO " "
  cd LibDockBot
  smake <CLEAN> DIR=<DIR> CPU=<CPU> DEF_DEBUG=$DEFDEBUG
  cd /
ENDIF

IF <NOEXE$EXE> EQ EXE
  echo Compiling DockBot
  echo " "
  cd DockBot
  smake <CLEAN> DIR=<DIR> CPU=<CPU> DEF_DEBUG_MEM=$DEFDEBUGMEM DEF_DEBUG=$DEFDEBUG
  cd /
ENDIF

IF <NOPREFS$PREFS> EQ PREFS
  echo Compiling DockBotPrefs
  echo " "
  cd DockBotPrefs
  smake <CLEAN> DIR=<DIR> CPU=<CPU> DEF_DEBUG=$DEFDEBUG
  cd /
ENDIF

IF <NOCLASSES$CLASSES> EQ CLASSES

  IF NOT EXISTS <DIR>/Gadgets
    MAKEDIR <DIR>/Gadgets
  ENDIF

  echo Compiling Classes/Button
  echo " "
  cd Classes/Button
  smake <CLEAN> DIR=<DIR> CPU=<CPU> DEF_DEBUG=$DEFDEBUG
  cd //

  echo Compiling Classes/Clock
  echo " "
  cd Classes/Clock
  smake <CLEAN> DIR=<DIR> CPU=<CPU> DEF_DEBUG=$DEFDEBUG
  cd //

  echo Compiling Classes/Mem
  echo " "
  cd Classes/Mem
  smake <CLEAN> DIR=<DIR> CPU=<CPU> DEF_DEBUG=$DEFDEBUG
  cd //
ENDIF

